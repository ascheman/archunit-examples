[[structure:Default]]
[role=group,includesConstraints="dependency:PackageCycles(minor), dependency:WrongDirection(critical)"]
- Package cycles
- <<dependency:WrongDirection>>

[[package:PackageLevel]]
.Set the level property of a package, e.g. 1 for de, 2 for de.kontext_e and so on.
[source,cypher,role=concept,requiresConcepts="dependency:Package"]
----
MATCH
    (p:Java:Package) // // <1>
WITH
    SPLIT(p.fqn, ".") AS splitted, p // // <2>
SET
    p.level=SIZE(splitted) // // <3>
RETURN
    splitted, SIZE(splitted);
----

[[dependency:TransitivePackageDependencies]]
.Add a DEPENDS_ON relationship to parents of a package P from other packages up to the same level of the source package.
[source,cypher,role=concept,requiresConcepts="package:PackageLevel"]
----
MATCH
    (p:Java:Package)-[:DEPENDS_ON]->(p2:Java:Package),
    (parent:Java:Package)-[:CONTAINS*]->(p2:Java:Package)
WHERE
    p.level <= parent.level
CREATE UNIQUE
    (p)-[:DEPENDS_ON]->(parent)
RETURN
    p.fqn, parent.fqn;
----

[[documented:TechnicalDebt]]
.Creates a relationship between two Packages for Technical Debt.
[source,cypher,role=concept]
----
match (a:Asciidoc:Table)-[:BODY]->(body)
where a.title='Unwantend Module Dependencies'
with body
match (c1:Asciidoc:Cell {colnumber: 0})<-[:CONTAINS_CELLS]-(body)-[:CONTAINS_CELLS]->(c2:Asciidoc:Cell {colnumber: 1})
with c1, c2
MATCH
    (m1:Java:Package), (m2:Java:Package)
WHERE
    m1.fqn = c1.text
AND
    m2.fqn = c2.text
MERGE
    (m1)-[:TECHNICAL_DEBT]->(m2)
RETURN
    m1, m2;
----

[[dependency:WrongDirection]]
.Finds package dependencies which are in the wrong direction according to the documentation.
[source,cypher,role=constraint,requiresConcepts="dependency:TransitivePackageDependencies, documented:TechnicalDebt",severity=critical]
----
MATCH
    (p1:PlantUml:Package)-[:MAY_DEPEND_ON]->(p2:PlantUml:Package), // // <1>
    (p3:Java:Package)-[:DEPENDS_ON]->(p4:Java:Package) // // <2>
WHERE
    p1.fqn = p4.fqn // // <3>
    AND p2.fqn = p3.fqn // // <4>
    AND NOT (p3)-[:TECHNICAL_DEBT]->(p4) // // <5>
RETURN
    p3.fqn + "-->" + p4.fqn AS WrongDirection; // // <6>
----
